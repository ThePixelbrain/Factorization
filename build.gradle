buildscript {
	repositories {
		mavenCentral()
		maven { url "https://maven.fabricmc.net" }
		maven { url "https://repo.sleeping.town" }
	}
	dependencies {
		classpath "agency.highlysuspect:voldeloom:2.4" 
	}
}

apply plugin: "agency.highlysuspect.voldeloom"

java.toolchain.languageVersion = JavaLanguageVersion.of(11) //Last version able to set a --release as low as 6
compileJava.options.release.set(6) //Forge doesn't understand classes compiled to versions of the class-file format newer than Java 6's

String minecraftVersion = "1.4.7"
String forgeVersion = "1.4.7-6.6.2.534"

archivesBaseName = "Factorization"
version = "0.7.21.1"

repositories {
	ivy {
		name 'asie dependency mirror'
		artifactPattern "https://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
		metadataSources {
			artifact()
		}
	}
}

sourceSets {
	main {
		java.srcDirs = ["src/main/java"]
		resources.srcDirs = ["src/main/resources", "docs"]
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	forge "net.minecraftforge:forge:${forgeVersion}:universal@zip"
	mappings "net.minecraftforge:forge:${forgeVersion}:src@zip"

	modCompileOnly name: "CodeChickenCore", version: "0.8.1.6", ext: "jar"
	modCompileOnly name: "NotEnoughItems", version: "1.4.7.4", ext: "jar"
}

volde {
	//more configuration goes here...
}

jar {
	manifest {
		attributes FMLCorePlugin: "factorization.client.coremod.BaseTransformer"
	}
}
